buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'

idea.module.inheritOutputDirs = true

def BUILD_ID = System.getenv('CI_PIPELINE_IID')
version = '0.3.0' + (BUILD_ID != null ? "+${BUILD_ID}" : '')
group = 'ru.mrkekman'
archivesBaseName = 'BloodyBranch'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
    mavenCentral()

    maven {
        name = "JEI"
        url "https://dvs1.progwml6.com/files/maven"
    }

    maven {
        name = "Curse"
        url = "https://www.cursemaven.com/"
    }

    maven {
        name = "Craft/CraftTweaker"
        url "https://maven.blamejared.com"
    }
}

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'
    accessTransformers = [file('src/main/resources/META-INF/jei_at.cfg')]
    runs {
        client {
//            args '--username', 'MrKekMan04'
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:22.0.0'
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    compileOnly 'CraftTweaker2:CraftTweaker2-API:4.1.20.656'
    compileOnly 'CraftTweaker2:ZenScript:4.1.20.656'
    compileOnly 'com.blamejared:MTLib:3.0.5.15'
    runtimeOnly 'com.blamejared:MTLib:3.0.5.15'
    runtimeOnly 'CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.656'
    compileOnly 'mezz.jei:jei_1.12.2:4.16.1.302:api'
    runtimeOnly fg.deobf('mezz.jei:jei_1.12.2:4.16.1.302')


    compile fg.deobf('curse.maven:bm-224791:2822288')
    compile fg.deobf('curse.maven:ga-228832:2645992')
}

jar.finalizedBy('reobfJar')

idea {
    module {
        inheritOutputDirs = true
    }
}

task prepareAssets(type: Copy) {
    group = 'build'

    from project.file('src/main/resources')
    into project.file('build/classes/java/main')
}

classes.dependsOn(prepareAssets)


processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version
    }
}



